# Test proper shutdown while "overflowing" warning and system messages

set test "warn_overflow"
if {![installtest_p]} { untested $test; return }

# --vp 00002 suppresses WARNING duplication filtering in staprun.
# The syscall.* probe handlers try to overflow the cmd message buffers
# which might take some time, so add -DMAXSKIPPED=9999 to not error
# out because some probes take too long.
spawn stap --vp 00002 -DMAXSKIPPED=9999 $srcdir/$subdir/$test.stp
set ok 0
set warn 0
expect {
    -timeout 180

    -re {^WARNING: too many pending \([a-z]+\) messages\r\n} {
        incr warn; exp_continue
    }

    -re {^stap_begin\r\n} { incr ok; exp_continue; }
    -re {^stap_syscall\r\n} { incr ok; exp_continue; }
    -re {^stap_end\r\n} { incr ok; exp_continue; }

    -re {^[^\r\n]*\r\n} { exp_continue }
    timeout { fail "$test (timeout)" }
    eof { }
}
catch { close }; catch { wait }

# There can be more than 3 "too many pending" messages in case
# stap recovered quick enough and then overflowed again.
if {$ok == 3 && $warn >= 3} {
    pass "$test ($ok,$warn)"
} {
    fail "$test ($ok,$warn)"
}
