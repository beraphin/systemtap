# statfs _____________________________________________________
# long sys_statfs(const char __user * path, struct statfs __user * buf)
# long compat_sys_statfs(const char __user *path, struct compat_statfs __user *buf)
#

@define _SYSCALL_STATFS_NAME
%(
	name = "statfs"
%)

@define _SYSCALL_STATFS_ARGSTR
%(
	argstr = sprintf("%s, %p", path, buf_uaddr)
%)

probe syscall.statfs = dw_syscall.statfs !, nd_syscall.statfs ? {}
probe syscall.statfs.return = dw_syscall.statfs.return !,
                              nd_syscall.statfs.return ? {}

# dw_statfs _____________________________________________________

probe dw_syscall.statfs = kernel.function("compat_sys_statfs").call ?,
                       kernel.function("sys_statfs").call ?
{
	@_SYSCALL_STATFS_NAME
	buf_uaddr = @__pointer($buf)
	path = user_string_quoted(@__pointer(@choose_defined($pathname, $path)))
	@_SYSCALL_STATFS_ARGSTR
}

probe dw_syscall.statfs.return = kernel.function("compat_sys_statfs").return ?,
                              kernel.function("sys_statfs").return ?
{
	@_SYSCALL_STATFS_NAME
	retstr = return_str(1, $return)
}

# nd_statfs _____________________________________________________

probe nd_syscall.statfs = kprobe.function("compat_sys_statfs") ?,
                          kprobe.function("sys_statfs") ?
{
	@_SYSCALL_STATFS_NAME
	asmlinkage()
	path = user_string_quoted(pointer_arg(1))
	buf_uaddr = pointer_arg(2)
	@_SYSCALL_STATFS_ARGSTR
}
probe nd_syscall.statfs.return = kprobe.function("compat_sys_statfs").return ?,
                                 kprobe.function("sys_statfs").return ?
{
	@_SYSCALL_STATFS_NAME
	retstr = returnstr(1)
}
